* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Program MOYA--P1.
/**
/** :author Administrator
/* TODO Enter your code here
* 
* Program for the function Modify / Delete Yacht
* -----------------------------------------------------
DEFINE DATA
GLOBAL USING ST----G0
LOCAL USING LAYOUT-L
LOCAL USING YA---ML0
LOCAL USING YA---VL0
local
1 #retry  (a1)
END-DEFINE
* ----------------------------------------------------
*
INCLUDE DFPF--C0
*
MOVE *PROGRAM TO #PROG
MOVE '  Modify Yacht' TO #TITLE
* ----------------------------------------------------
REPEAT
  MOVE (AD=I) TO #KEY-C
  MOVE (AD=P) TO #DATA-C
  INPUT WITH TEXT #G-MESSAGE-TEXT  USING MAP 'MAYA--M1'
  RESET #G-MESSAGE
*
  DECIDE FOR FIRST CONDITION
    WHEN *PF-KEY = 'PF3' OR = 'PF12'
      FETCH 'MEYA--P0'
    WHEN *PF-KEY = 'ENTR'
*
      IF YACHT-M.YACHT-ID = 0
        REINPUT 'Please enter Yacht-ID > 0'
      END-IF
*
      PERFORM PROCESS-RECORD
    WHEN NONE
      REINPUT 'Invalid PF-KEY '
  END-DECIDE
END-REPEAT
*
* ----------------------------------------------------
ON ERROR
  DECIDE ON FIRST VALUE OF  *ERROR-NR
    VALUE  3145          /* HOLD
      INPUT (AD=MT) 'Record is in hold status - Retry (Y/N)' #RETRY
      IF  #RETRY = 'Y'    /* User request
        RETRY
      ELSE
        BACKOUT TRANSACTION
        FETCH 'MOYA--P1'
      END-IF
    VALUE 3148
      INPUT (AD=M)//// '    Database is not active - start DB'
      STOP
    VALUE 1305
      INPUT (AD=M)//// '    Numeric value truncation - correct program'
      STOP
    NONE VALUE
      IGNORE            /*  Call of central error routine
  END-DECIDE
END-ERROR
* ====================================================
*   S U B R O U T I N E S
* ----------------------------------------------------
*
DEFINE SUBROUTINE PROCESS-RECORD
FIND YACHT-V WITH YACHT-ID = YACHT-M.YACHT-ID
  IF NO RECORDS FOUND
    REINPUT 'Yacht-ID does not exist'
  END-NOREC
  MOVE BY NAME YACHT-V TO YACHT-M
  REPEAT
    MOVE (AD=P) TO #KEY-C
    MOVE (AD=I) TO #DATA-C
    INPUT USING MAP 'MAYA--M1'
    DECIDE FOR FIRST CONDITION
      WHEN *PF-KEY = 'ENTR'
        IGNORE
      WHEN *PF-KEY = 'PF3'
        MOVE BY NAME YACHT-M TO YACHT-V
        UPDATE /* DELETE
        END TRANSACTION
        RESET YACHT-M
        ESCAPE ROUTINE
      WHEN *PF-KEY = 'PF12'
        BACKOUT TRANSACTION
        FETCH 'MEYA--P0'
      WHEN NONE
        REINPUT 'Invalid PF-KEY'
    END-DECIDE
*
  END-REPEAT
END-FIND
END-SUBROUTINE
*
END
